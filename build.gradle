import com.github.spotbugs.snom.SpotBugsTask

plugins {
    id 'io.spring.dependency-management' version "$rev_spring_dependency_management" apply false
    id 'info.solidsoft.pitest' version "$rev_pit_test" apply false
    id 'com.github.spotbugs' version "$rev_spotbugs" apply false
}

def checkstyleConfigs = layout.projectDirectory.dir("config/checkstyle")
def checkstyleRules = checkstyleConfigs.file("checkstyle.xml").asFile
def checkstyleReport = resources.text.fromFile(checkstyleConfigs.file("checkstyle-report.xsl"))

subprojects {

    group = 'org.bananalaba.springcdtemplate'
    version = "$rev_project"

    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'

    sourceCompatibility = '1.20'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    test {
        useJUnitPlatform()
    }

    dependencyManagement {
        dependencies {
            dependency "org.apache.commons:commons-lang3:$rev_commons_lang"
            dependency "com.google.guava:guava:$rev_guava"

            dependency "com.fasterxml.jackson.core:jackson-databind:$rev_jackson"

            dependency "org.projectlombok:lombok:$rev_lombok"
            dependency "org.projectlombok:lombok:$rev_lombok"

            dependency "org.projectlombok:lombok:$rev_lombok"
            dependency "org.projectlombok:lombok:$rev_lombok"

            dependency "org.junit.jupiter:junit-jupiter:$rev_junit"
            dependency "org.mockito:mockito-core:$rev_mockito"
            dependency "org.mockito:mockito-junit-jupiter:$rev_mockito"
            dependency "org.assertj:assertj-core:$rev_assertj"

            dependency "com.h3xstream.findsecbugs:findsecbugs-plugin:$rev_findsecbugs"
        }
    }

    dependencies {
        compileOnly "org.projectlombok:lombok"
        annotationProcessor "org.projectlombok:lombok"

        testCompileOnly "org.projectlombok:lombok"
        testAnnotationProcessor "org.projectlombok:lombok"

        testImplementation "org.junit.jupiter:junit-jupiter"
        testImplementation "org.mockito:mockito-core"
        testImplementation "org.mockito:mockito-junit-jupiter"
        testImplementation "org.assertj:assertj-core"

        spotbugsPlugins "com.h3xstream.findsecbugs:findsecbugs-plugin"
    }

    tasks.withType(Checkstyle) {
        configFile = checkstyleRules
        reports {
            html.stylesheet = checkstyleReport
        }
    }

    tasks.withType(SpotBugsTask) {
        reports {
            html {
                required = true
            }
        }
    }

}
