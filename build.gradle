import com.github.spotbugs.snom.SpotBugsTask

plugins {
    id 'info.solidsoft.pitest' version "$rev_pit_test" apply false
    id 'com.github.spotbugs' version "$rev_spotbugs_plugin" apply false
    id 'org.owasp.dependencycheck' version "$rev_owasp_dependency_check" apply false
    id 'org.springframework.boot' version "$rev_spring_boot" apply false
    id 'io.spring.dependency-management' version "$rev_spring_dependency_management" apply false
}

def checkstyleConfigs = layout.projectDirectory.dir("config/checkstyle")
def checkstyleRules = checkstyleConfigs.file("checkstyle.xml").asFile
def checkstyleReport = resources.text.fromFile(checkstyleConfigs.file("checkstyle-report.xsl"))

def spotBugsConfigs = layout.projectDirectory.dir("config/spotbugs")
def spotBugsExclusions = spotBugsConfigs.file("exclude.xml").asFile

subprojects {

    group = 'org.bananalaba.springcdtemplate'
    version = "$rev_project"

    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'org.owasp.dependencycheck'

    sourceCompatibility = '1.23'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    test {
        useJUnitPlatform()
    }

    dependencies {
        compileOnly libs.spotbugs.annotations

        compileOnly libs.lombok
        annotationProcessor libs.lombok

        testCompileOnly libs.lombok
        testAnnotationProcessor libs.lombok

        testImplementation libs.junit.jupiter
        testImplementation libs.mockito.core
        testImplementation libs.mockito.junit
        testImplementation libs.assertj

        spotbugs "com.github.spotbugs:spotbugs:$rev_spotbugs_tool"
        spotbugs libs.commons.lang.build
        spotbugsPlugins libs.find.sec.bugs
    }

    tasks.withType(Checkstyle).configureEach {
        configFile = checkstyleRules
        reports {
            html.stylesheet = checkstyleReport
        }
    }

    def spotBugsEnabled = Boolean.parseBoolean(System.getProperty("spotBugsEnabled", "true"))
    spotbugs {
        excludeFilter.set(
            spotBugsExclusions
        )
        ignoreFailures = true
    }
    tasks.withType(SpotBugsTask).configureEach {
        enabled = spotBugsEnabled
        reports {
            html {
                required = true
            }
        }
    }

    dependencyCheck {
        failBuildOnCVSS = 10
    }

}

tasks.register("buildImages") {
    dependsOn(":data-service:jibDockerBuild", ":sample-application:jibDockerBuild")
}
