plugins {
    id 'io.spring.dependency-management'
}

sourceSets {
    acceptanceTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output

        java {
        }
    }
}

configurations {
    acceptanceTestImplementation.extendsFrom(testImplementation)
    acceptanceTestRuntimeOnly.extendsFrom(testRuntimeOnly)
}

dependencyManagement {
    imports {
        mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
}

dependencies {
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.boot:spring-boot-starter-web"

    acceptanceTestImplementation libs.junit.platform.suite

    acceptanceTestImplementation libs.cucumber.java
    acceptanceTestImplementation libs.cucumber.junit
    acceptanceTestImplementation libs.cucumber.spring

    testImplementation libs.jsonassert
}

tasks.register("acceptanceTest", Test) {
    description = "Run acceptance tests"
    group = "verification"
    testClassesDirs = sourceSets.acceptanceTest.output.classesDirs
    classpath = sourceSets.acceptanceTest.runtimeClasspath

    useJUnitPlatform()
}
