plugins {
    id 'org.springframework.boot'
    id 'info.solidsoft.pitest'
    id 'maven-publish'
    id 'io.spring.dependency-management'
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output

        java {
        }
    }
}

configurations {
    integrationTestImplementation.extendsFrom(testImplementation)
    integrationTestRuntimeOnly.extendsFrom(testRuntimeOnly)
}

pitest {
    pitestVersion = '1.14.4'
    junit5PluginVersion = '1.0.0'
    threads = 4
    useClasspathFile = true
    outputFormats = ['XML', 'HTML']
}

dependencies {
    implementation libs.commons.lang
    implementation libs.guava

    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-aop"

    implementation "io.micrometer:micrometer-tracing"
    implementation "io.micrometer:micrometer-tracing-bridge-otel"
    implementation "io.opentelemetry:opentelemetry-exporter-otlp"

    implementation "org.springframework.boot:spring-boot-starter-cache"
    implementation "org.springframework.boot:spring-boot-starter-data-redis"
    implementation libs.cache.caffeine

    implementation project(":sample-model")

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation libs.testContainers.redis
    testImplementation libs.testContainers.core
}

tasks.register("itest", Test) {
    description = "Run integration tests"
    group = "verification"
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    useJUnitPlatform()
}

tasks.named('build').configure {
    dependsOn itest
}

tasks.named('bootBuildImage') {
    imageName = 'ci-cd-project/sample-application'
}

publishing {
    publications {
        bootJava(MavenPublication) {
            artifact tasks.named("bootJar")
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/JudzinBaranovsky/spring-boot-cd-skeleton"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
