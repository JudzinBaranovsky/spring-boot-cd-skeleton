buildscript {
    dependencies {
        classpath("org.ow2.asm:asm:9.7")
    }
}

plugins {
    id 'org.springframework.boot'
    id 'info.solidsoft.pitest'
    id 'maven-publish'
    id 'io.spring.dependency-management'
    id 'com.google.cloud.tools.jib' version '3.3.2'
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output

        java {
        }
    }
}

configurations {
    integrationTestImplementation.extendsFrom(testImplementation)
    integrationTestRuntimeOnly.extendsFrom(testRuntimeOnly)
}

pitest {
    pitestVersion = '1.17.1'
    junit5PluginVersion = '1.2.1'
    threads = 4
    useClasspathFile = true
    outputFormats = ['XML', 'HTML']
}

dependencies {
    implementation libs.commons.lang
    implementation libs.guava

    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-aop"

    implementation "io.micrometer:micrometer-tracing"
    implementation "io.micrometer:micrometer-tracing-bridge-otel"
    implementation "io.opentelemetry:opentelemetry-exporter-otlp"

    implementation "org.codehaus.janino:janino:3.1.8"

    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    runtimeOnly 'org.postgresql:postgresql'
    implementation "org.flywaydb:flyway-database-postgresql"

    implementation project(":sample-model")

    testImplementation "org.springframework.boot:spring-boot-starter-test"

    integrationTestImplementation 'org.springframework.boot:spring-boot-testcontainers'
    integrationTestImplementation 'org.testcontainers:junit-jupiter'
    integrationTestImplementation 'org.testcontainers:postgresql'

    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0"

    implementation "org.mapstruct:mapstruct:${rev_mapstruct}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${rev_mapstruct}"
}

tasks.register("itest", Test) {
    description = "Run integration tests"
    group = "verification"
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    useJUnitPlatform()
}

tasks.named('build').configure {
    dependsOn itest
}

jib {
    from {
        image = "eclipse-temurin:23-jre-alpine"
    }
    to {
        image = "ci-cd-project/sample-application:latest"
    }
}

publishing {
    publications {
        bootJava(MavenPublication) {
            artifact tasks.named("bootJar")
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/JudzinBaranovsky/spring-boot-cd-skeleton"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
